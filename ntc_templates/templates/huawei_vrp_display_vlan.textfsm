Value Filldown VLAN_ID (\d+)
Value Filldown VLAN_TYPE (\w+)
Value Filldown MODE ((TG|UT))
Value INTERFACE ([A-Z0-9/th\-runk]+)
Value VLAN_NAME (\S.+\S|\S+)
Value VLAN_STATUS (\S+)
Value VLAN_BROADCAST (\S+)
Value VLAN_MULTICAST (\S+)
Value VLAN_UNICAST (\S+)
Value VLAN_PROPERTY (\S+)
Value VLAN_MAC_LEARNING (\S+)
Value VLAN_STATISTICS (\S+)

Start
  ^\s*\*\s+:\s+management-vlan\s*$$
  ^\s*The\s+total\s+number\s+of\s+(VLANs|vlans)\s+is\s*:\s+\d+\s*$$
  ^-+$$
  ^U:\s+Up;\s+D:\s+Down;\s+TG:\s+Tagged;\s+UT:\s+Untagged;
  ^MP:\s+Vlan-mapping;\s+ST:\s+Vlan-stacking;
  ^#:\s+ProtocolTransparent-vlan;\s+\*:\s+Management-vlan;
  ^VID\s+Type\s+Ports -> VLANS
  ^VID\s+Status\s+Property -> STATUS
  ^\s*VLAN\s+ID\s+Type\s+Status\s+MAC\s+Learning\s+Broadcast\/Multicast\/Unicast\s+Property\s*$$ -> VLANS_AP
  ^. -> Error

VLANS
  ^-+$$
  ^${VLAN_ID}\s+${VLAN_TYPE}\s*$$ -> Record
  ^${VLAN_ID}\s+${VLAN_TYPE}\s+${MODE}:${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^${VLAN_ID}\s+${VLAN_TYPE}\s+${MODE}:(?:\S+\s+){1}\s+${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^${VLAN_ID}\s+${VLAN_TYPE}\s+${MODE}:(?:\S+\s+){2}\s+${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^${VLAN_ID}\s+${VLAN_TYPE}\s+${MODE}:(?:\S+\s+){3}\s+${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^${VLAN_ID}\s+${VLAN_TYPE}\s+${MODE}:(?:\S+\s+){4}\s+${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\d+
  ^\s{6,}${MODE}:${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}(?:\S+\s+){1}${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}(?:\S+\s+){2}${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}(?:\S+\s+){3}${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}(?:\S+\s+){4}${INTERFACE}\(\w+\)\s* -> Continue.Record
  ^\s{6,}
  ^\s*$$ -> Start
  ^. -> Error


STATUS
  ^\s*-+$$ -> Clearall
  ^\s*${VLAN_ID}\s+${VLAN_STATUS}\s+${VLAN_PROPERTY}\s+${VLAN_MAC_LEARNING}\s+${VLAN_STATISTICS}\s+${VLAN_NAME}\s*$$ -> Continue.Record
  ^\s*\d+ -> Clearall
  ^. -> Error


VLANS_AP
  ^\s*-+$$ -> Clearall
  ^\s*${VLAN_ID}\s+${VLAN_TYPE}\s+${VLAN_STATUS}\s+${VLAN_MAC_LEARNING}\s+${VLAN_BROADCAST}\s+${VLAN_MULTICAST}\s+${VLAN_UNICAST}\s+${VLAN_PROPERTY}\s*$$ -> Continue.Record
  ^\s*\d+ -> Clearall
  ^. -> Error